buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        mavenCentral()
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:5.+"
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "com.github.johnrengelman.shadow"

configurations {
    include { transitive = false }
    implementation.extendsFrom(include)
}

minecraft {
    mappings channel: "snapshot", version: "20210309-1.16.5"

    runs {
        client {
            workingDirectory project.file("run")
        }
    }
}

dependencies {
    // Built against 1.16 since 1.17 requires java 16 which would obviously break backwards compatibility
    minecraft "net.minecraftforge:forge:1.16.5-36.2.4"

    include project(":common")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

jar {
    enabled = false
}

shadowJar {
    archiveClassifier.set("")

    configurations = [project.configurations.include]
}

jar.dependsOn(shadowJar)